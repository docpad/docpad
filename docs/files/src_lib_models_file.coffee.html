<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>src/lib/models/file.coffee</title>
    <link rel="stylesheet" href="http://yui.yahooapis.com/3.9.1/build/cssgrids/cssgrids-min.css">
    <link rel="stylesheet" href="../assets/vendor/prettify/prettify-min.css">
    <link rel="stylesheet" href="../assets/css/main.css" id="site_styles">
    <link rel="icon" href="../assets/favicon.ico">
    <script src="http://yui.yahooapis.com/combo?3.9.1/build/yui/yui-min.js"></script>
</head>
<body class="yui3-skin-sam">

<div id="doc">
    <div id="hd" class="yui3-g header">
        <div class="yui3-u-3-4">
                <h1><img src="../assets/css/logo.png" title="" width="117" height="52"></h1>
        </div>
        <div class="yui3-u-1-4 version">
            <em>API Docs for: </em>
        </div>
    </div>
    <div id="bd" class="yui3-g">

        <div class="yui3-u-1-4">
            <div id="docs-sidebar" class="sidebar apidocs">
                <div id="api-list">
                    <h2 class="off-left">APIs</h2>
                    <div id="api-tabview" class="tabview">
                        <ul class="tabs">
                            <li><a href="#api-classes">Classes</a></li>
                            <li><a href="#api-modules">Modules</a></li>
                        </ul>
                
                        <div id="api-tabview-filter">
                            <input type="search" id="api-filter" placeholder="Type to filter APIs">
                        </div>
                
                        <div id="api-tabview-panel">
                            <ul id="api-classes" class="apis classes">
                                <li><a href="../classes/BasePlugin.html">BasePlugin</a></li>
                                <li><a href="../classes/Collection.html">Collection</a></li>
                                <li><a href="../classes/Docpad.html">Docpad</a></li>
                                <li><a href="../classes/docpadUtil.html">docpadUtil</a></li>
                                <li><a href="../classes/DocumentModel.html">DocumentModel</a></li>
                                <li><a href="../classes/ElementsCollection.html">ElementsCollection</a></li>
                                <li><a href="../classes/Events.html">Events</a></li>
                                <li><a href="../classes/FileModel.html">FileModel</a></li>
                                <li><a href="../classes/FilesCollection.html">FilesCollection</a></li>
                                <li><a href="../classes/MetaCollection.html">MetaCollection</a></li>
                                <li><a href="../classes/Model.html">Model</a></li>
                                <li><a href="../classes/PluginLoader.html">PluginLoader</a></li>
                                <li><a href="../classes/PluginTester.html">PluginTester</a></li>
                                <li><a href="../classes/QueryCollection.html">QueryCollection</a></li>
                                <li><a href="../classes/ScriptCollection.html">ScriptCollection</a></li>
                                <li><a href="../classes/ServerTester.html">ServerTester</a></li>
                                <li><a href="../classes/StylesCollection.html">StylesCollection</a></li>
                            </ul>
                
                
                            <ul id="api-modules" class="apis modules">
                            </ul>
                        </div>
                    </div>
                </div>
            </div>
        </div>
        <div class="yui3-u-3-4">
                <div id="api-options">
                    Show:
                    <label for="api-show-inherited">
                        <input type="checkbox" id="api-show-inherited" checked>
                        Inherited
                    </label>
            
                    <label for="api-show-protected">
                        <input type="checkbox" id="api-show-protected">
                        Protected
                    </label>
            
                    <label for="api-show-private">
                        <input type="checkbox" id="api-show-private">
                        Private
                    </label>
                    <label for="api-show-deprecated">
                        <input type="checkbox" id="api-show-deprecated">
                        Deprecated
                    </label>
            
                </div>
            
            <div class="apidocs">
                <div id="docs-main">
                    <div class="content">
<h1 class="file-heading">File: src/lib/models/file.coffee</h1>

<div class="file">
    <pre class="code prettyprint linenums">
# =====================================
# Requires

# Standard Library
util = require(&#x27;util&#x27;)
pathUtil = require(&#x27;path&#x27;)

# External
isTextOrBinary = require(&#x27;istextorbinary&#x27;)
typeChecker = require(&#x27;typechecker&#x27;)
{TaskGroup} = require(&#x27;taskgroup&#x27;)
safefs = require(&#x27;safefs&#x27;)
mime = require(&#x27;mime&#x27;)
extendr = require(&#x27;extendr&#x27;)
extractOptsAndCallback = require(&#x27;extract-opts&#x27;)

# Optional
jschardet = null
encodingUtil = null

# Local
{Model} = require(&#x27;../base&#x27;)
docpadUtil = require(&#x27;../util&#x27;)


# =====================================
# Classes

###*
# The FileModel class is DocPad&#x27;s representation
# of a file in the file system.
# Extends the DocPad Model class
# https://github.com/docpad/docpad/blob/master/src/lib/base.coffee#L49.
# FileModel manages the loading
# of a file and parsing both the content and the metadata (if any).
# Once loaded, the content, metadata and file stat (file info)
# properties of the FileModel are populated, as well
# as a number of DocPad specific attributes and properties.
# Typically we do not need to create FileModels ourselves as
# DocPad handles all of that. But it is possible that a plugin
# may need to manually create FileModels for some reason.
#
#	attrs =
#		fullPath: &#x27;file/path/to/somewhere&#x27;
#	opts = {}
#	#we only really need the path to the source file to create
#	#a new file model
#	model = new FileModel(attrs, opts)
#
# The FileModel forms the base class for the DocPad DocumentModel class.
# @class FileModel
# @constructor
# @extends Model
###
class FileModel extends Model

	# ---------------------------------
	# Properties

	###*
	# The file model class. This should
	# be overridden in any descending classes.
	# @private
	# @property {Object} klass
	###
	klass: FileModel

	###*
	# String name of the model type.
	# In this case, &#x27;file&#x27;. This should
	# be overridden in any descending classes.
	# @private
	# @property {String} type
	###
	type: &#x27;file&#x27;

	###*
	# Task Group Class
	# @private
	# @property {Object} TaskGroup
	###
	TaskGroup: null

	###*
	# The out directory path to put the relative path.
	# @property {String} rootOutDirPath
	###
	rootOutDirPath: null

	###*
	# Whether or not we should detect encoding
	# @property {Boolean} detectEncoding
	###
	detectEncoding: false

	###*
	# Node.js file stat object.
	# https://nodejs.org/api/fs.html#fs_class_fs_stats.
	# Basically, information about a file, including file
	# dates and size.
	# @property {Object} stat
	###
	stat: null

	###*
	# File buffer. Node.js Buffer object.
	# https://nodejs.org/api/buffer.html#buffer_class_buffer.
	# Provides methods for dealing with binary data directly.
	# @property {Object} buffer
	###
	buffer: null

	###*
	# Buffer time.
	# @property {Object} bufferTime
	###
	bufferTime: null

	###*
	# The parsed file meta data (header).
	# Is a Model instance.
	# @private
	# @property {Object} meta
	###
	meta: null

	###*
	# Locale information for the file
	# @private
	# @property {Object} locale
	###
	locale: null
	###*
	# Get the file&#x27;s locale information
	# @method getLocale
	# @return {Object} the locale
	###
	getLocale: -&gt; @locale

	###*
	# Get Options. Returns an object containing
	# the properties detectEncoding, rootOutDirPath
	# locale, stat, buffer, meta and TaskGroup.
	# @private
	# @method getOptions
	# @return {Object}
	###
	# @TODO: why does this not use the isOption way?
	getOptions: -&gt;
		return {@detectEncoding, @rootOutDirPath, @locale, @stat, @buffer, @meta, @TaskGroup}

	###*
	# Checks whether the passed key is one
	# of the options.
	# @private
	# @method isOption
	# @param {String} key
	# @return {Boolean}
	###
	isOption: (key) -&gt;
		names = [&#x27;detectEncoding&#x27;, &#x27;rootOutDirPath&#x27;, &#x27;locale&#x27;, &#x27;stat&#x27;, &#x27;data&#x27;, &#x27;buffer&#x27;, &#x27;meta&#x27;, &#x27;TaskGroup&#x27;]
		result = key in names
		return result

	###*
	# Extract Options.
	# @private
	# @method extractOptions
	# @param {Object} attrs
	# @return {Object} the options object
	###
	extractOptions: (attrs) -&gt;
		# Prepare
		result = {}

		# Extract
		for own key,value of attrs
			if @isOption(key)
				result[key] = value
				delete attrs[key]

		# Return
		return result

	###*
	# Set the options for the file model.
	# Valid properties for the attrs parameter:
	# TaskGroup, detectEncoding, rootOutDirPath,
	# locale, stat, data, buffer, meta.
	# @method setOptions
	# @param {Object} [attrs={}]
	###
	setOptions: (attrs={}) -&gt;
		# TaskGroup
		if attrs.TaskGroup?
			@TaskGroup = attrs.TaskGroup
			delete @attributes.TaskGroup

		# Root Out Path
		if attrs.detectEncoding?
			@rootOutDirPath = attrs.detectEncoding
			delete @attributes.detectEncoding

		# Root Out Path
		if attrs.rootOutDirPath?
			@rootOutDirPath = attrs.rootOutDirPath
			delete @attributes.rootOutDirPath

		# Locale
		if attrs.locale?
			@locale = attrs.locale
			delete @attributes.locale

		# Stat
		if attrs.stat?
			@setStat(attrs.stat)
			delete @attributes.stat

		# Data
		if attrs.data?
			@setBuffer(attrs.data)
			delete @attributes.data

		# Buffer
		if attrs.buffer?
			@setBuffer(attrs.buffer)
			delete @attributes.buffer

		# Meta
		if attrs.meta?
			@setMeta(attrs.meta)
			delete @attributes.meta

		# Chain
		@

	###*
	# Clone the model and return the newly cloned model.
	# @method clone
	# @return {Object} cloned file model
	###
	clone: -&gt;
		# Fetch
		attrs = @getAttributes()
		opts = @getOptions()

		# Clean up
		delete attrs.id
		delete attrs.meta.id
		delete opts.meta.id
		delete opts.meta.attributes.id

		# Clone
		clonedModel = new @klass(attrs, opts)

		# Emit clone event so parent can re-attach listeners
		@emit(&#x27;clone&#x27;, clonedModel)

		# Return
		return clonedModel


	# ---------------------------------
	# Attributes

	###*
	# The default attributes for any file model.
	# @private
	# @property {Object}
	###
	defaults:

		# ---------------------------------
		# Automaticly set variables

		# The unique document identifier
		id: null

		# The file&#x27;s name without the extension
		basename: null

		# The out file&#x27;s name without the extension
		outBasename: null

		# The file&#x27;s last extension
		# &quot;hello.md.eco&quot; -&gt; &quot;eco&quot;
		extension: null

		# The extension used for our output file
		outExtension: null

		# The file&#x27;s extensions as an array
		# &quot;hello.md.eco&quot; -&gt; [&quot;md&quot;,&quot;eco&quot;]
		extensions: null  # Array

		# The file&#x27;s name with the extension
		filename: null

		# The full path of our source file, only necessary if called by @load
		fullPath: null

		# The full directory path of our source file
		fullDirPath: null

		# The output path of our file
		outPath: null

		# The output path of our file&#x27;s directory
		outDirPath: null

		# The file&#x27;s name with the rendered extension
		outFilename: null

		# The relative path of our source file (with extensions)
		relativePath: null

		# The relative output path of our file
		relativeOutPath: null

		# The relative directory path of our source file
		relativeDirPath: null

		# The relative output path of our file&#x27;s directory
		relativeOutDirPath: null

		# The relative base of our source file (no extension)
		relativeBase: null

		# The relative base of our out file (no extension)
		relativeOutBase: null

		# The MIME content-type for the source file
		contentType: null

		# The MIME content-type for the out file
		outContentType: null

		# The date object for when this document was created
		ctime: null

		# The date object for when this document was last modified
		mtime: null

		# The date object for when this document was last rendered
		rtime: null

		# The date object for when this document was last written
		wtime: null

		# Does the file actually exist on the file system
		exists: null


		# ---------------------------------
		# Content variables

		# The encoding of the file
		encoding: null

		# The raw contents of the file, stored as a String
		source: null

		# The contents of the file, stored as a String
		content: null


		# ---------------------------------
		# User set variables

		# The tags for this document
		tags: null  # CSV/Array

		# Whether or not we should render this file
		render: false

		# Whether or not we should write this file to the output directory
		write: true

		# Whether or not we should write this file to the source directory
		writeSource: false

		# Whether or not this file should be re-rendered on each request
		dynamic: false

		# The title for this document
		# Useful for page headings
		title: null

		# The name for this document, defaults to the outFilename
		# Useful for navigation listings
		name: null

		# The date object for this document, defaults to mtime
		date: null

		# The generated slug (url safe seo title) for this document
		slug: null

		# The url for this document
		url: null

		# Alternative urls for this document
		urls: null  # Array

		# Whether or not we ignore this file
		ignored: false

		# Whether or not we should treat this file as standalone (that nothing depends on it)
		standalone: false



	# ---------------------------------
	# Helpers

	###*
	# File encoding helper
	# opts = {path, to, from, content}
	# @private
	# @method encode
	# @param {Object} opts
	# @return {Object} encoded result
	###
	encode: (opts) -&gt;
		# Prepare
		locale = @getLocale()
		result = opts.content
		opts.to ?= &#x27;utf8&#x27;
		opts.from ?= &#x27;utf8&#x27;

		# Import optional dependencies
		try encodingUtil ?= require(&#x27;encoding&#x27;)

		# Convert
		if encodingUtil?
			@log &#x27;info&#x27;, util.format(locale.fileEncode, opts.to, opts.from, opts.path)
			try
				result = encodingUtil.convert(opts.content, opts.to, opts.from)
			catch err
				@log &#x27;warn&#x27;, util.format(locale.fileEncodeConvertError, opts.to, opts.from, opts.path)
		else
			@log &#x27;warn&#x27;, util.format(locale.fileEncodeConvertError, opts.to, opts.from, opts.path)

		# Return
		return result

	###*
	# Set the file model&#x27;s buffer.
	# Creates a new node.js buffer
	# object if a buffer object is
	# is not passed as the parameter
	# @method setBuffer
	# @param {Object} [buffer]
	###
	setBuffer: (buffer) -&gt;
		buffer = new Buffer(buffer)  unless Buffer.isBuffer(buffer)
		@bufferTime = @get(&#x27;mtime&#x27;) or new Date()
		@buffer = buffer
		@

	###*
	# Get the file model&#x27;s buffer object.
	# Returns a node.js buffer object.
	# @method getBuffer
	# @return {Object} node.js buffer object
	###
	getBuffer: -&gt;
		return @buffer

	###*
	# Is Buffer Outdated
	# True if there is no buffer OR the buffer time is outdated
	# @method isBufferOutdated
	# @return {Boolean}
	###
	isBufferOutdated: -&gt;
		return @buffer? is false or @bufferTime &lt; (@get(&#x27;mtime&#x27;) or new Date())

	###*
	# Set the node.js file stat.
	# @method setStat
	# @param {Object} stat
	###
	setStat: (stat) -&gt;
		@stat = stat
		@set(
			ctime: new Date(stat.ctime)
			mtime: new Date(stat.mtime)
		)
		@

	###*
	# Get the node.js file stat.
	# @method getStat
	# @return {Object} the file stat
	###
	getStat: -&gt;
		return @stat

	###*
	# Get the file model attributes.
	# By default the attributes will be
	# dereferenced from the file model.
	# To maintain a reference, pass false
	# as the parameter. The returned object
	# will NOT contain the file model&#x27;s ID attribute.
	# @method getAttributes
	# @param {Object} [dereference=true]
	# @return {Object}
	###
	#NOTE: will the file model&#x27;s ID be deleted if
	#dereference=false is passed??
	getAttributes: (dereference=true) -&gt;
		attrs = @toJSON(dereference)
		delete attrs.id
		return attrs

	###*
	# Get the file model attributes.
	# By default the attributes will
	# maintain a reference to the file model.
	# To return a dereferenced object, pass true
	# as the parameter. The returned object
	# will contain the file model&#x27;s ID attribute.
	# @method toJSON
	# @param {Object} [dereference=false]
	# @return {Object}
	###
	toJSON: (dereference=false) -&gt;
		data = super
		data.meta = @getMeta().toJSON()
		data = extendr.dereference(data)  if dereference is true
		return data

	###*
	# Get the file model metadata object.
	# Optionally pass a list of metadata property
	# names corresponding to those properties that
	# you want returned.
	# @method getMeta
	# @param {Object} [args...]
	# @return {Object}
	###
	getMeta: (args...) -&gt;
		@meta = new Model()  if @meta is null
		if args.length
			return @meta.get(args...)
		else
			return @meta

	###*
	# Assign attributes and options to the file model.
	# @method set
	# @param {Array} attrs the attributes to be applied
	# @param {Object} opts the options to be applied
	###
	set: (attrs,opts) -&gt;
		# Check
		if typeChecker.isString(attrs)
			newAttrs = {}
			newAttrs[attrs] = opts
			return @set(newAttrs, opts)

		# Prepare
		attrs = attrs.toJSON?() ? attrs

		# Extract options
		options = @extractOptions(attrs)

		# Perform the set
		super(attrs, opts)

		# Apply the options
		@setOptions(options, opts)

		# Chain
		@

	###*
	# Set defaults. Apply default attributes
	# and options to the file model
	# @method setDefaults
	# @param {Object} attrs the attributes to be applied
	# @param {Object} opts the options to be applied
	###
	setDefaults: (attrs,opts) -&gt;
		# Prepare
		attrs = attrs.toJSON?() ? attrs

		# Extract options
		options = @extractOptions(attrs)

		# Apply
		super(attrs, opts)

		# Apply the options
		@setOptions(options, opts)

		# Chain
		return @

	###*
	# Set the file model meta data,
	# attributes and options in one go.
	# @method setMeta
	# @param {Object} attrs the attributes to be applied
	# @param {Object} opts the options to be applied
	###
	setMeta: (attrs,opts) -&gt;
		# Prepare
		attrs = attrs.toJSON?() ? attrs

		# Extract options
		options = @extractOptions(attrs)

		# Apply
		@getMeta().set(attrs, opts)
		@set(attrs, opts)

		# Apply the options
		@setOptions(options, opts)

		# Chain
		return @


	###*
	# Set the file model meta data defaults
	# @method setMetaDefaults
	# @param {Object} attrs the attributes to be applied
	# @param {Object} opts the options to be applied
	###
	setMetaDefaults: (attrs,opts) -&gt;
		# Prepare
		attrs = attrs.toJSON?() ? attrs

		# Extract options
		options = @extractOptions(attrs)

		# Apply
		@getMeta().setDefaults(attrs, opts)
		@setDefaults(attrs, opts)

		# Apply the options
		@setOptions(options, opts)

		# Chain
		return @

	###*
	# Get the file name. Depending on the
	# parameters passed this will either be
	# the file model&#x27;s filename property or,
	# the filename determined from the fullPath
	# or relativePath property. Valid values for
	# the opts parameter are: fullPath, relativePath
	# or filename. Format: {filename}
	# @method getFilename
	# @param {Object} [opts={}]
	# @return {String}
	###
	getFilename: (opts={}) -&gt;
		# Prepare
		{fullPath,relativePath,filename} = opts

		# Determine
		result = (filename ? @get(&#x27;filename&#x27;))
		if !result
			result = (fullPath ? @get(&#x27;fullPath&#x27;)) or (relativePath ? @get(&#x27;relativePath&#x27;))
			result = pathUtil.basename(result)  if result
		result or= null

		# REturn
		return result

	###*
	# Get the file path. Depending on the
	# parameters passed this will either be
	# the file model&#x27;s fullPath property, the
	# relativePath property or the filename property.
	# Valid values for the opts parameter are:
	# fullPath, relativePath
	# or filename. Format: {fullPath}
	# @method getFilePath
	# @param {Object} [opts={}]
	# @return {String}
	###
	getFilePath: (opts={}) -&gt;
		# Prepare
		{fullPath,relativePath,filename} = opts

		# Determine
		result = (fullPath ? @get(&#x27;fullPath&#x27;)) or (relativePath ? @get(&#x27;relativePath&#x27;)) or (filename ? @get(&#x27;filename&#x27;)) or null

		# Return
		return result

	###*
	# Get file extensions. Depending on the
	# parameters passed this will either be
	# the file model&#x27;s extensions property or
	# the extensions extracted from the file model&#x27;s
	# filename property. The opts parameter is passed
	# in the format: {extensions,filename}.
	# @method getExtensions
	# @param {Object} opts
	# @return {Array} array of extension names
	###
	getExtensions: ({extensions,filename}) -&gt;
		extensions or= @get(&#x27;extensions&#x27;) or null
		if (extensions or []).length is 0
			filename = @getFilename({filename})
			if filename
				extensions = docpadUtil.getExtensions(filename)
		return extensions or null

	###*
	# Get the file content. This will be
	# the text content if loaded or the file buffer object.
	# @method getContent
	# @return {String or Object}
	###
	getContent: -&gt;
		return @get(&#x27;content&#x27;) or @getBuffer()

	###*
	# Get the file content for output.
	# @method getOutContent
	# @return {String or Object}
	###
	getOutContent: -&gt;
		return @getContent()

	###*
	# Is this a text file? ie - not
	# a binary file.
	# @method isText
	# @return {Boolean}
	###
	isText: -&gt;
		return @get(&#x27;encoding&#x27;) isnt &#x27;binary&#x27;

	###*
	# Is this a binary file?
	# @method isBinary
	# @return {Boolean}
	###
	isBinary: -&gt;
		return @get(&#x27;encoding&#x27;) is &#x27;binary&#x27;

	###*
	# Set the url for the file
	# @method setUrl
	# @param {String} url
	###
	setUrl: (url) -&gt;
		@addUrl(url)
		@set({url})
		@

	###*
	# A file can have multiple urls.
	# This method adds either a single url
	# or an array of urls to the file model.
	# @method addUrl
	# @param {String or Array} url
	###
	addUrl: (url) -&gt;
		# Multiple Urls
		if url instanceof Array
			for newUrl in url
				@addUrl(newUrl)

		# Single Url
		else if url
			found = false
			urls = @get(&#x27;urls&#x27;)
			for existingUrl in urls
				if existingUrl is url
					found = true
					break
			urls.push(url)  if not found
			@trigger(&#x27;change:urls&#x27;, @, urls, {})
			@trigger(&#x27;change&#x27;, @, {})

		# Chain
		@

	###*
	# Removes a url from the file
	# model (files can have more than one url).
	# @method removeUrl
	# @param {Object} userUrl the url to be removed
	###
	removeUrl: (userUrl) -&gt;
		urls = @get(&#x27;urls&#x27;)
		for url,index in urls
			if url is userUrl
				urls.splice(index,1)
				break
		@

	###*
	# Get a file path.
	# If the relativePath parameter starts with &#x60;.&#x60; then we get the
	# path in relation to the document that is calling it.
	# Otherwise we just return it as normal
	# @method getPath
	# @param {String} relativePath
	# @param {String} parentPath
	# @return {String}
	###
	getPath: (relativePath, parentPath) -&gt;
		if /^\./.test(relativePath)
			relativeDirPath = @get(&#x27;relativeDirPath&#x27;)
			path = pathUtil.join(relativeDirPath, relativePath)
		else
			if parentPath
				path = pathUtil.join(parentPath, relativePath)
			else
				path = relativePath
		return path


	# ---------------------------------
	# Actions

	###*
	# The action runner instance bound to DocPad
	# @private
	# @property {Object} actionRunnerInstance
	###
	actionRunnerInstance: null
	###*
	# Get the action runner instance bound to DocPad
	# @method getActionRunner
	# @return {Object}
	###
	getActionRunner: -&gt; @actionRunnerInstance
	###*
	# Apply an action with the supplied arguments.
	# @method action
	# @param {Object} args...
	###
	action: (args...) =&gt; docpadUtil.action.apply(@, args)

	###*
	# Initialize the file model with the passed
	# attributes and options. Emits the init event.
	# @method initialize
	# @param {Object} attrs the file model attributes
	# @param {Object} [opts={}] the file model options
	###
	initialize: (attrs,opts={}) -&gt;
		# Defaults
		file = @
		@attributes ?= {}
		@attributes.extensions ?= []
		@attributes.urls ?= []
		now = new Date()
		@attributes.ctime ?= now
		@attributes.mtime ?= now

		# Id
		@id ?= @attributes.id ?= @cid

		# Options
		@setOptions(opts)

		# Error
		if @rootOutDirPath? is false or @locale? is false
			throw new Error(&quot;Use docpad.createModel to create the file or document model&quot;)

		# Create our action runner
		@actionRunnerInstance = new @TaskGroup(&quot;file action runner&quot;).whenDone (err) -&gt;
			file.emit(&#x27;error&#x27;, err)  if err

		# Apply
		@emit(&#x27;init&#x27;)

		# Chain
		@

	###*
	# Load the file from the file system.
	# If the fullPath exists, load the file.
	# If it doesn&#x27;t, then parse and normalize the file.
	# Optionally pass file options as a parameter.
	# @method load
	# @param {Object} [opts={}]
	# @param {Function} next callback
	###
	load: (opts={},next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts,next)
		file = @
		opts.exists ?= null

		# Fetch
		fullPath = @get(&#x27;fullPath&#x27;)
		filePath = @getFilePath({fullPath})

		# Apply options
		file.set(exists: opts.exists)  if opts.exists?
		file.setStat(opts.stat)        if opts.stat?
		file.setBuffer(opts.buffer)    if opts.buffer?

		# Tasks
		tasks = new @TaskGroup(&quot;load tasks for file: #{filePath}&quot;, {next})
			.on(&#x27;item.run&#x27;, (item) -&gt;
				file.log(&quot;debug&quot;, &quot;#{item.getConfig().name}: #{file.type}: #{filePath}&quot;)
			)

		# Detect the file
		tasks.addTask &quot;Detect the file&quot;, (complete) -&gt;
			if fullPath and opts.exists is null
				safefs.exists fullPath, (exists) -&gt;
					opts.exists = exists
					file.set(exists: opts.exists)
					return complete()
			else
				return complete()

		tasks.addTask &quot;Stat the file and cache the result&quot;, (complete) -&gt;
			# Otherwise fetch new stat
			if fullPath and opts.exists and opts.stat? is false
				return safefs.stat fullPath, (err,fileStat) -&gt;
					return complete(err)  if err
					file.setStat(fileStat)
					return complete()
			else
				return complete()

		# Process the file
		tasks.addTask &quot;Read the file and cache the result&quot;, (complete) -&gt;
			# Otherwise fetch new buffer
			if fullPath and opts.exists and opts.buffer? is false and file.isBufferOutdated()
				return safefs.readFile fullPath, (err,buffer) -&gt;
					return complete(err)  if err
					file.setBuffer(buffer)
					return complete()
			else
				return complete()

		tasks.addTask &quot;Load -&gt; Parse&quot;, (complete) -&gt;
			file.parse(complete)

		tasks.addTask &quot;Parse -&gt; Normalize&quot;, (complete) -&gt;
			file.normalize(complete)

		tasks.addTask &quot;Normalize -&gt; Contextualize&quot;, (complete) -&gt;
			file.contextualize(complete)

		# Run the tasks
		tasks.run()

		# Chain
		@

	###*
	# Parse our buffer and extract meaningful data from it.
	# next(err).
	# @method parse
	# @param {Object} [opts={}]
	# @param {Object} next callback
	###
	parse: (opts={},next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		buffer = @getBuffer()
		relativePath = @get(&#x27;relativePath&#x27;)
		encoding = opts.encoding or @get(&#x27;encoding&#x27;) or null
		changes = {}

		# Detect Encoding
		if buffer and encoding? is false or opts.reencode is true
			isText = isTextOrBinary.isTextSync(relativePath, buffer)

			# Text
			if isText is true
				# Detect source encoding if not manually specified
				if @detectEncoding
					jschardet ?= require(&#x27;jschardet&#x27;)
					encoding ?= jschardet.detect(buffer)?.encoding

				# Default the encoding
				encoding or= &#x27;utf8&#x27;

				# Convert into utf8
				if docpadUtil.isStandardEncoding(encoding) is false
					buffer = @encode({
						path: relativePath
						to: &#x27;utf8&#x27;
						from: encoding
						content: buffer
					})

				# Apply
				changes.encoding = encoding

			# Binary
			else
				# Set
				encoding = changes.encoding = &#x27;binary&#x27;

		# Binary
		if encoding is &#x27;binary&#x27;
			# Set
			content = source = &#x27;&#x27;

			# Apply
			changes.content = content
			changes.source = source

		# Text
		else
			# Default
			encoding = changes.encoding = &#x27;utf8&#x27;  if encoding? is false

			# Set
			source = buffer?.toString(&#x27;utf8&#x27;) or &#x27;&#x27;
			content = source

			# Apply
			changes.content = content
			changes.source = source

		# Apply
		@set(changes)

		# Next
		next()
		@

	###*
	# Normalize any parsing we have done, because if a value has
	# updates it may have consequences on another value.
	# This will ensure everything is okay.
	# next(err)
	# @method normalize
	# @param {Object} [opts={}]
	# @param {Object} next callback
	###
	normalize: (opts={},next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts,next)
		changes = {}
		meta = @getMeta()
		locale = @getLocale()

		# App specified
		filename = opts.filename or @get(&#x27;filename&#x27;) or null
		relativePath = opts.relativePath or @get(&#x27;relativePath&#x27;) or null
		fullPath = opts.fullPath or @get(&#x27;fullPath&#x27;) or null
		mtime = opts.mtime or @get(&#x27;mtime&#x27;) or null

		# User specified
		tags = opts.tags or meta.get(&#x27;tags&#x27;) or null
		date = opts.date or meta.get(&#x27;date&#x27;) or null
		name = opts.name or meta.get(&#x27;name&#x27;) or null
		slug = opts.slug or meta.get(&#x27;slug&#x27;) or null
		url = opts.url or meta.get(&#x27;url&#x27;) or null
		contentType = opts.contentType or meta.get(&#x27;contentType&#x27;) or null
		outContentType = opts.outContentType or meta.get(&#x27;outContentType&#x27;) or null
		outFilename = opts.outFilename or meta.get(&#x27;outFilename&#x27;) or null
		outExtension = opts.outExtension or meta.get(&#x27;outExtension&#x27;) or null
		outPath = opts.outPath or meta.get(&#x27;outPath&#x27;) or null

		# Force specifeid
		extensions = null
		extension = null
		basename = null
		outBasename = null
		relativeOutPath = null
		relativeDirPath = null
		relativeOutDirPath = null
		relativeBase = null
		relativeOutBase = null
		outDirPath = null
		fullDirPath = null

		# filename
		changes.filename = filename = @getFilename({filename, relativePath, fullPath})

		# check
		if !filename
			err = new Error(locale.filenameMissingError)
			return next(err)

		# relativePath
		if !relativePath and filename
			changes.relativePath = relativePath = filename

		# force basename
		changes.basename = basename = docpadUtil.getBasename(filename)

		# force extensions
		changes.extensions = extensions = @getExtensions({filename})

		# force extension
		changes.extension = extension = docpadUtil.getExtension(extensions)

		# force fullDirPath
		if fullPath
			changes.fullDirPath = fullDirPath = docpadUtil.getDirPath(fullPath)

		# force relativeDirPath
		changes.relativeDirPath = relativeDirPath = docpadUtil.getDirPath(relativePath)

		# force relativeBase
		changes.relativeBase = relativeBase =
			if relativeDirPath
				pathUtil.join(relativeDirPath, basename)
			else
				basename

		# force contentType
		if !contentType
			changes.contentType = contentType = mime.lookup(fullPath or relativePath)

		# adjust tags
		if tags and typeChecker.isArray(tags) is false
			changes.tags = tags = String(tags).split(/[\s,]+/)

		# force date
		if !date
			changes.date = date = mtime or @get(&#x27;date&#x27;) or new Date()

		# force outFilename
		if !outFilename and !outPath
			changes.outFilename = outFilename = docpadUtil.getOutFilename(basename, outExtension or extensions.join(&#x27;.&#x27;))

		# force outPath
		if !outPath
			changes.outPath = outPath =
				if @rootOutDirPath
					pathUtil.resolve(@rootOutDirPath, relativeDirPath, outFilename)
				else
					null
			# ^ we still do this set as outPath is a meta, and it may still be set as an attribute

		# refresh outFilename
		if outPath
			changes.outFilename = outFilename = docpadUtil.getFilename(outPath)

		# force outDirPath
		changes.outDirPath = outDirPath =
			if outPath
				docpadUtil.getDirPath(outPath)
			else
				null

		# force outBasename
		changes.outBasename = outBasename = docpadUtil.getBasename(outFilename)

		# force outExtension
		changes.outExtension = outExtension = docpadUtil.getExtension(outFilename)

		# force relativeOutPath
		changes.relativeOutPath = relativeOutPath =
			if outPath
				outPath.replace(@rootOutDirPath, &#x27;&#x27;).replace(/^[\/\\]/, &#x27;&#x27;)
			else
				pathUtil.join(relativeDirPath, outFilename)

		# force relativeOutDirPath
		changes.relativeOutDirPath = relativeOutDirPath = docpadUtil.getDirPath(relativeOutPath)

		# force relativeOutBase
		changes.relativeOutBase = relativeOutBase = pathUtil.join(relativeOutDirPath, outBasename)

		# force name
		if !name
			changes.name = name = outFilename

		# force url
		_defaultUrl = docpadUtil.getUrl(relativeOutPath)
		if url
			@setUrl(url)
			@addUrl(_defaultUrl)
		else
			@setUrl(_defaultUrl)

		# force outContentType
		if !outContentType and contentType
			changes.outContentType = outContentType = mime.lookup(outPath or relativeOutPath) or contentType

		# force slug
		if !slug
			changes.slug = slug = docpadUtil.getSlug(relativeOutBase)

		# Force date objects
		changes.wtime = wtime = new Date(wtime)  if typeof wtime is &#x27;string&#x27;
		changes.rtime = rtime = new Date(rtime)  if typeof rtime is &#x27;string&#x27;
		changes.ctime = ctime = new Date(ctime)  if typeof ctime is &#x27;string&#x27;
		changes.mtime = mtime = new Date(mtime)  if typeof mtime is &#x27;string&#x27;
		changes.date  = date  = new Date(date)   if typeof date is &#x27;string&#x27;

		# Apply
		@set(changes)

		# Next
		next()
		@

	###*
	# Contextualize the data. In other words,
	# put our data into the perspective of the bigger picture of the data.
	# For instance, generate the url for it&#x27;s rendered equivalant.
	# next(err)
	# @method contextualize
	# @param {Object} [opts={}]
	# @param {Object} next callback
	###
	contextualize: (opts={},next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts,next)

		# Forward
		next()
		@

	###*
	# Render this file. The file model output content is
	# returned to the passed callback in the
	# result (2nd) parameter. The file model itself is returned
	# in the callback&#x27;s document (3rd) parameter.
	# next(err,result,document)
	# @method render
	# @param {Object} [opts={}]
	# @param {Object} next callback
	###
	render: (opts={},next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		file = @

		# Apply
		file.attributes.rtime = new Date()

		# Forward
		next(null, file.getOutContent(), file)
		@


	# ---------------------------------
	# CRUD

	###*
	# Write the out file. The out file
	# may be different from the input file.
	# Often the input file is transformed in some way
	# and saved as another file format. A common example
	# is transforming a markdown input file to a HTML
	# output file.
	# next(err)
	# @method write
	# @param {Object} opts
	# @param {Function} next callback
	###
	write: (opts,next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		file = @
		locale = @getLocale()

		# Fetch
		opts.path      or= file.get(&#x27;outPath&#x27;)
		opts.encoding  or= file.get(&#x27;encoding&#x27;) or &#x27;utf8&#x27;
		opts.content   or= file.getOutContent()
		opts.type      or= &#x27;out file&#x27;

		# Check
		# Sometimes the out path could not be set if we are early on in the process
		unless opts.path
			next()
			return @

		# Convert utf8 to original encoding
		unless opts.encoding.toLowerCase() in [&#x27;ascii&#x27;,&#x27;utf8&#x27;,&#x27;utf-8&#x27;,&#x27;binary&#x27;]
			opts.content = @encode({
				path: opts.path
				to: opts.encoding
				from: &#x27;utf8&#x27;
				content: opts.content
			})

		# Log
		file.log &#x27;debug&#x27;, util.format(locale.fileWrite, opts.type, opts.path, opts.encoding)

		# Write data
		safefs.writeFile opts.path, opts.content, (err) -&gt;
			# Check
			return next(err)  if err

			# Update the wtime
			if opts.type is &#x27;out file&#x27;
				file.attributes.wtime = new Date()

			# Log
			file.log &#x27;debug&#x27;,  util.format(locale.fileWrote, opts.type, opts.path, opts.encoding)

			# Next
			return next()

		# Chain
		@

	###*
	# Write the source file. Optionally pass
	# the opts parameter to modify or set the file&#x27;s
	# path, content or type.
	# next(err)
	# @method writeSource
	# @param {Object} [opts]
	# @param {Object} next callback
	###
	writeSource: (opts,next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		file = @

		# Fetch
		opts.path      or= file.get(&#x27;fullPath&#x27;)
		opts.content   or= (file.getContent() or &#x27;&#x27;).toString(&#x27;&#x27;)
		opts.type      or= &#x27;source file&#x27;

		# Write data
		@write(opts, next)

		# Chain
		@

	###*
	# Delete the out file, perhaps ahead of regeneration.
	# Optionally pass the opts parameter to set the file path or type.
	# next(err)
	# @method delete
	# @param {Object} [opts]
	# @param {Object} next callback
	###
	&#x27;delete&#x27;: (opts,next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		file = @
		locale = @getLocale()

		# Fetch
		opts.path      or= file.get(&#x27;outPath&#x27;)
		opts.type      or= &#x27;out file&#x27;

		# Check
		# Sometimes the out path could not be set if we are early on in the process
		unless opts.path
			next()
			return @

		# Log
		file.log &#x27;debug&#x27;,  util.format(locale.fileDelete, opts.type, opts.path)

		# Check existance
		safefs.exists opts.path, (exists) -&gt;
			# Exit if it doesn&#x27;t exist
			return next()  unless exists

			# If it does exist delete it
			safefs.unlink opts.path, (err) -&gt;
				# Check
				return next(err)  if err

				# Log
				file.log &#x27;debug&#x27;, util.format(locale.fileDeleted, opts.type, opts.path)

				# Next
				next()

		# Chain
		@

	###*
	# Delete the source file.
	# Optionally pass the opts parameter to set the file path or type.
	# next(err)
	# @method deleteSource
	# @param {Object} [opts]
	# @param {Object} next callback
	###
	deleteSource: (opts,next) -&gt;
		# Prepare
		[opts,next] = extractOptsAndCallback(opts, next)
		file = @

		# Fetch
		opts.path      or= file.get(&#x27;fullPath&#x27;)
		opts.type      or= &#x27;source file&#x27;

		# Write data
		@delete(opts, next)

		# Chain
		@


# ---------------------------------
# Export
module.exports = FileModel

    </pre>
</div>
                    </div>
                </div>
            </div>
        </div>
    </div>
</div>
<script src="../assets/vendor/prettify/prettify-min.js"></script>
<script>prettyPrint();</script>
<script src="../assets/js/yui-prettify.js"></script>
<script src="../assets/../api.js"></script>
<script src="../assets/js/api-filter.js"></script>
<script src="../assets/js/api-list.js"></script>
<script src="../assets/js/api-search.js"></script>
<script src="../assets/js/apidocs.js"></script>
</body>
</html>
